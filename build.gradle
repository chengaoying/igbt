apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'

ext.env = System.getenv()
ext.props = project.properties

sourceCompatibility = '1.3'
targetCompatibility = '1.1'
group = validprop('jmeGroup')
archivesBaseName = property('jmeArchivesBaseName')
version = validprop('jmeVersion')

defaultTasks 'uberjme'

def validprop(name) {
	if (project.hasProperty(name)) {
		project.property(name)
	}
	else {
		''
	}
}

def renderTemplate(text, binding) {
	def engine = new groovy.text.SimpleTemplateEngine()
	def template = engine.createTemplate(text).make(binding)
	template.toString()
}

def renderTemplateFile(f, binding) {
	def text = f instanceof File ? f.getText() : file(f).getText()
	renderTemplate(text, binding)
}

def writeAppDesc(f) {
	def file = f instanceof File ? f : file(f)
	file.write """MIDlet-Version: 1.0.0
MIDlet-Vendor: MIDlet Suite Vendor
MIDlet-Jar-URL: ${uberjme.archiveName}
MicroEdition-Configuration: CLDC-1.1
MicroEdition-Profile: MIDP-2.0
MIDlet-Name: ${archivesBaseName} MIDlet Suite
"""
	if (project.hasProperty('midlet')) {
		file.append("MIDlet-1: ${archivesBaseName}MIDlet,,${midlet}\r\n")
	}
}

def jadName(name) {
	def str = name.toString()
	str[0..<str.length()-1]+"d"
}

configurations {
	jme
	wtkProvide
	compile.extendsFrom(wtkProvide)
}

repositories {
	flatDir name: 'wtk', dirs: "${env.WTK_HOME}/lib"
	mavenLocal()
}

dependencies {
	wtkProvide ':cldcapi11:@jar'
	wtkProvide ':midpapi20:@jar'
}

compileJava {
	if (project.hasProperty('srcEncoding')) {
		options.encoding = srcEncoding
	}
	options.bootClasspath = configurations.wtkProvide.inject(""){a, b -> a+b+':'}
}

task preverify(type: Exec) {
	dependsOn compileJava
	ext.destinationPath = "${buildDir}/iptv/classes"
	executable "${env.WTK_HOME}/bin/preverify"
	args '-classpath', configurations.compile.inject(""){a, b -> a+b+';'}
	args '-d', destinationPath
	args '-target', 'CLDC1.1'
	args compileJava.destinationDir
}

task resjme(type: Copy) {
  dependsOn processResources
  from processResources.destinationDir
  into "${buildDir}/iptv/resources"
  ext.confDir = "$destinationDir/conf"
  ext.confPath = "$confDir/game.conf"
}

resjme << {
	if (project.hasProperty('telcom')&&project.hasProperty('sp')&&project.hasProperty('midlet')) {
		mkdir(confDir)
		if (file("${env.IGBT_HOME}/conf/${telcom}-${sp}.conf").exists()) {
			def text = renderTemplateFile("${env.IGBT_HOME}/conf/${telcom}-${sp}.conf", props)
			file(confPath).write(text)
		}
		else {
			def text1 = renderTemplateFile("${env.IGBT_HOME}/conf/${telcom}.conf", props)
			def text2 = renderTemplateFile("${env.IGBT_HOME}/conf/${sp}.conf", props)
			file(confPath).write(text1+text2)
		}
	}
}

task jarjme(type: Jar) {
	dependsOn preverify
  dependsOn resjme
	from preverify.destinationPath
	from resjme.destinationDir
	destinationDir = file("${buildDir}/iptv/jar")
}

task uberjme(type: Jar) {
	dependsOn preverify
  dependsOn resjme
	from configurations.refProvide.collect { it.isDirectory() ? it : zipTree(it) }
	from preverify.destinationPath
	from resjme.destinationDir
	destinationDir = file("${buildDir}/iptv/uberjar")
	ext.jadName = jadName(uberjme.archiveName)
	ext.jadPath = jadName(uberjme.archivePath)
	doLast {
		writeAppDesc(jadPath)
	}
}

task obfuscate(type: Exec) {
	dependsOn uberjme
	ext.destinationPath = "${buildDir}/iptv/publish"
	ext.archivePath = destinationPath+"/"+uberjme.archiveName
	executable "${env.PROGUARD_HOME}/bin/proguard.bat"
	args '-injars', uberjme.archivePath
	args '-outjars', archivePath
	configurations.wtkProvide.each {args '-libraryjars', it }
	args "-repackageclasses ''"
	args '-overloadaggressively'
	args '-microedition'
	args '-keep', 'public class * extends javax.microedition.midlet.MIDlet'
	doLast {
		copy {
			from uberjme.jadPath
			into destinationPath
		}
	}
}

task publish {
	dependsOn obfuscate
	doLast {
	
	}
}

task testjme {
	dependsOn test
	dependsOn uberjme
}

testjme << {
	if (project.hasProperty('telcom')&&project.hasProperty('sp')&&project.hasProperty('midlet')) {
		if (file("${env.IGBT_HOME}/jads/${telcom}-${sp}.jad").exists()) {
			def text = renderTemplateFile("${env.IGBT_HOME}/jads/${telcom}-${sp}.jad", props)
			file(uberjme.jadPath).append(text)
		}
		else {
			def text = renderTemplateFile("${env.IGBT_HOME}/jads/${sp}.jad", props)
			file(uberjme.jadPath).append(text)
		}
	}
}

testjme << {
	ant.java(classname: 'org.microemu.app.Main', fork: true, spawn: true) {
		arg(value: '--resizableDevice 640 530')
		arg(value: uberjme.jadPath)
		classpath {
			pathelement(location: "${env.IGBT_HOME}/libs/microemulator.jar")
			pathelement(location: uberjme.archivePath)
		}
	}
}

task pngout(type: Exec) {
	executable "${env.IGBT_HOME}/bin/pngout.bat"
	sourceSets.main.resources.srcDirs.each{ args it }
}

artifacts {
  jme uberjme.archivePath
}

uploadJme {
	dependsOn uberjme
	repositories {
    mavenDeployer {
  		if (validprop('releaseRepos')) {
        repository(url: releaseRepos) {
        	if (validprop('releaseReposUser')&&validprop('releaseReposPasswd')) {
        		authentication(userName: releaseReposUser, password: releaseReposPasswd)
        	}
        }
      }
      if (validprop('snapshotRepos')) {
        snapshotRepository(url: snapshotRepos) {
        	if (validprop('snapshotReposUser')&&validprop('snapshotReposPasswd')) {
        		authentication(userName: snapshotReposUser, password: snapshotReposPasswd)
        	}
        }
      }
    }
  }
}

task mtj {
	dependsOn 'eclipse'
	doLast {
		writeAppDesc(file('Application Descriptor'))
	}
}

mtj << {
	file('.mtj').write """<?xml version="1.0" encoding="UTF-8"?>
<mtjMetadata version="1.2.1.v201009031435">
   <signing projectSpecific="false" signProject="false">
      <alias/>
   </signing>
   <configurations>
      <configuration active="true" name="iptv">
         <device group="Sun Java(TM) Wireless Toolkit 2.5.2_01 for CLDC" name="DefaultColorPhone"/>
         <symbolSet name="DefaultColorPhone">
            <symbol name="screen.isColor" value="true"/>
            <symbol name="MMAPI" value="1.1"/>
            <symbol name="screen.bitDepth" value="8"/>
            <symbol name="JSR82" value="1.1"/>
            <symbol name="JSR226" value="1.0"/>
            <symbol name="MIDP" value="2.0"/>
            <symbol name="JSR229" value="1.1"/>
            <symbol name="SATSA-PKI" value="1.0"/>
            <symbol name="CLDC" value="1.1"/>
            <symbol name="JSR179" value="1.0"/>
            <symbol name="WMA" value="2.0"/>
            <symbol name="SATSA-JCRMI" value="1.0"/>
            <symbol name="J2ME-WS" value="1.0"/>
            <symbol name="screen.width" value="240"/>
            <symbol name="JSR238" value="1.0"/>
            <symbol name="JSR239" value="1.0"/>
            <symbol name="screen.isTouch" value="false"/>
            <symbol name="JSR211" value="1.0"/>
            <symbol name="JSR234" value="1.0"/>
            <symbol name="SATSA-APDU" value="1.0"/>
            <symbol name="JSR75" value="1.0"/>
            <symbol name="J2ME-XMLRPC" value="1.0"/>
            <symbol name="JSR184" value="1.1"/>
            <symbol name="SATSA-CRYPTO" value="1.0"/>
            <symbol name="version.configuration" value="CLDC-1.1"/>
            <symbol name="version.profile" value="MIDP-2.1"/>
            <symbol name="JSR180" value="1.0"/>
            <symbol name="screen.height" value="320"/>
         </symbolSet>
      </configuration>
   </configurations>
</mtjMetadata>"""
}

eclipse.classpath.file {
    whenMerged { classpath ->
        classpath.entries.removeAll { entry -> entry.kind == 'con' }
    }
}

